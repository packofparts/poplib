plugins {
    id "java"
    id "java-library"
    id "edu.wpi.first.GradleRIO" version "2025.1.1"
    id "org.sonarqube" version "6.0.1.5171"
    id "maven-publish"
}

import java.util.regex.Matcher

def getVersion() {
    def build_name = System.getenv("BUILD_NAME")
    if (build_name != null) {
        return build_name
    }

    def tags = providers.exec {
      commandLine("git", "tag", "--list", "v*", "--sort=-v:refname")
    }.standardOutput.asText.get()

    // get the first tag (latest version) and strip the leading 'v' character
    return tags.split("\n")[0].substring(1) 
}

project.version = getVersion()
println "Project version set to ${project.version}"

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor wpi.java.deps.wpilibAnnotations()
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()
}

sonar {
  properties {
    property "sonar.projectKey", "packofparts_poplib"
    property "sonar.organization", "packofparts"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

publishing {
  publications {
    gpr(MavenPublication) {
      groupId = 'org.packofparts'
      artifactId = 'poplib'
      from(components.java)
    }
  }

  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/packofparts/poplib"
      credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }
}
